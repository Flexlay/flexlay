## -*- mode: python -*-
##  $Id$
##
##  Flexlay - A Generic 2D Game Editor
##  Copyright (C) 2002 Ingo Ruhnke <grumbel@gmx.de>
##
##  This program is free software; you can redistribute it and/or
##  modify it under the terms of the GNU General Public License
##  as published by the Free Software Foundation; either version 2
##  of the License, or (at your option) any later version.
##
##  This program is distributed in the hope that it will be useful,
##  but WITHOUT ANY WARRANTY; without even the implied warranty of
##  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
##  GNU General Public License for more details.
##
##  You should have received a copy of the GNU General Public License
##  along with this program; if not, write to the Free Software
##  Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.

import os
import re
import glob

opts = Options('custom.py')
opts.Add('CXX', 'The C++ compiler.', 'g++')
opts.Add('CXXFLAGS', 'The C++ compiler flags.', '-g -O2 -Wall')
opts.Add('CPPPATH', 'additional header path', '')
opts.Add('LIBPATH', 'additional library path', '')
opts.Add('RUBYDIR', 'Ruby directory', os.popen('ruby -e "require \'rbconfig\'; puts Config::CONFIG[\'archdir\']"').read()[:-1])

opts.Add('DESTDIR', 'destination directory which gets appended to PREFIX', '')
opts.Add('PREFIX',  'PREFIX, ie. /usr/local', '/usr/local')

env = Environment(SHLIBPREFIX='',
                  ENV = os.environ,
                  options=opts)
Help(opts.GenerateHelpText(env))

env.Command('swigrun_wrap.cxx', 'swigrun.i',
            "swig -runtime -ruby -o $TARGET -c++ $SOURCE")

env.SharedLibrary(target='libswigrun.so', source='swigrun_wrap.cxx',
              CPPPATH=['$USER_CPPPATH','$RUBYDIR'])

def gen_i_depends(path, filename):
    print os.getcwd()
    lst = []
    regex = re.compile("^%include +\"(.*)\".*")
    f = file(filename)
    for i in f.readlines():
        i = i[:-1]
        if regex.match(i):
            lst.append(path + "/" + regex.sub("\\1", i))
    f.close()
    return lst            

Depends('../lib/flexlay_wrap.i', gen_i_depends(Dir("../lib").abspath, File("../lib/flexlay_wrap.i").abspath))
env.Command('flexlay_ruby_wrap.cxx', '../lib/flexlay_wrap.i',
            "swig -noruntime -ruby -o $TARGET -c++ $SOURCE")

clanLib_env = Environment()
clanLib_env.ParseConfig("pkg-config --cflags --libs clanCore-0.7 clanDisplay-0.7 clanGL-0.7 clanSignals-0.7 clanGUI-0.7 clanGUIStyleSilver-0.7")

libflexlay_ruby_env = env.Copy()
flexlay_ruby_lib = libflexlay_ruby_env.SharedLibrary(
    target = 'flexlay_wrap.so',
    source = [
    'flexlay_ruby_wrap.cxx',
    'ruby_functor.cxx',
    'ruby_meta_data.cxx',
    'ruby_sexpr_parser.cxx',
    'ruby_object.cxx'
    ],
    CPPPATH=['$USER_CPPPATH','$RUBYDIR','../lib/'] + clanLib_env['CPPPATH'],
    LINKFLAGS = clanLib_env['LINKFLAGS'],
    LIBPATH=['../lib', '.'] + clanLib_env['LIBPATH'],
    LIBS=['swigrun', 'flexlay'] + clanLib_env['LIBS'])

# install_ruby = env.Alias('install_headers', Install(env['DESTDIR'] + env['PREFIX'] + '/include/flexlay', flexlay_headers))
# env.Alias('install', [install_ruby])

# EOF #

